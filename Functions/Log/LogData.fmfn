/**
 * =====================================
 * LogData ( theLogLevel ; theLogMessage )
 *
 * RETURNS:
 *		(string) Let format dictionary which describes the current environment.
 *
 * PARAMETERS:
 *		theLogLevel = (numeric) 0 thru [n]
 *		theLogMessage = (text, optional) some text describing the log entry
 *
 * EXAMPLE:
 *		LogData ( LogLevel2Warn ; "error when going to layout: 'Customer: Detail'" )
 *			// where LogLevel2Warn is a custom function which returns a number
 *		LogData ( $logLevelDebug ; "start section: create related records" )
 *			// where $logLevelDebug contains the numeric representation of a debug level log entry
 *		LogData ( 3 ; "script runtime: 0.3 seconds" )
 *
 * NOTES:
 *		This function should be modified to capture any relevant values for
 *		your solution.
 *
 * HISTORY:
 *		MODIFIED on 2014-OCT-02 by Daniel Smith dansmith65@gmail.com
 *			- add: AccountPrivilegeSetName, CurrentPrivilegeSetName, logLevelToWriteOffset, LOGLEVELTOWRITEOFFSETglobal
 *			- move UserName to the ALL LOG LEVELS section (helps to differentiate two users logged on with the same account)
 *			- include additional detail when log level is ≤ 2 rather than ≤ 1
 *			- change theMessage parameter to theLogMessage
 *			- encode theLogMessage parameter as logMessage instead of message
 *			- modify documentation/notes
 *		MODIFIED on 2014-FEB-05 by Daniel Smith dansmith65@gmail.com
 *			- prepend parameter with "the" to prevent collision
 *			- add theMessage parameter
 *			- remove session-specific values (see LogDataSession function)
 *			- manually encode name/value pairs, remove dependency on #
 *			- add values added in FileMaker 13
 *			- disable UserCount
 *		MODIFIED on 2014-JAN-11 matt@filemakermagazine.com to use numeric logLevel
 *		MODIFIED on 2012-NOV-21 by Daniel Smith dansmith65@gmail.com
 *			- don't access $error variable directly
 *		CREATED on 2012-NOV-21 Daniel Smith dansmith65@gmail.com
 *		INSPIRED by ErrorData by matt@filemakermagazine.com
 *			https://github.com/filemakerstandards/fmpstandards/tree/master/Functions
 *
 * REFERENCE:
 *		http://filemakerstandards.org/x/LACm
 * =====================================
 */

// ALL LOG LEVELS
// ============================================================================
"$LogLevel = " & theLogLevel & " ;¶"
& If ( not IsEmpty ( theLogMessage ) ;
	"$logMessage = " & Quote ( theLogMessage ) & " ;¶"
)
& "$AccountName = " & Quote ( Get ( AccountName ) ) & " ;¶"
& "$UserName = " & Quote ( Get ( UserName ) ) & " ;¶"
& "$CurrentHostTimestamp = GetAsTimeStamp ( " & Quote ( Get ( CurrentHostTimestamp ) ) & " ) ;¶"
// possibly use UTC in addition to/instead of CurrentHostTimeStamp, may want to only store this once per session
//    FM12: Get ( UTCmSecs )
//    FM13: Get ( CurrentTimeUTCMilliseconds )
& "$FileName = " & Quote ( Get ( FileName ) ) & " ;¶"
& "$ScriptName = " & Quote ( Get ( ScriptName ) ) & " ;¶"
& "$ScriptParameter = " & Quote ( Get ( ScriptParameter ) ) & " ;¶"
& "$ScriptResult = " & Quote ( Get ( ScriptResult ) ) & " ;¶"

// if account priv. set and current priv. set are different, there are two reasons I know of that can cause this:
//	1. script was run with Full Access privileges
//	2. account was changed while user was logged in and they have not logged out/in again
& "$AccountPrivilegeSetName = " & Quote ( Get ( AccountPrivilegeSetName ) ) & " ;¶"
& "$CurrentPrivilegeSetName = " & Quote ( Get ( CurrentPrivilegeSetName ) ) & " ;¶"

// used by LogWriteEnabled custom function:
& If ( not IsEmpty ( $logLevelToWriteOffset ) ;
	"$logLevelToWriteOffset = " & $logLevelToWriteOffset & " ;¶"
)
& If ( not IsEmpty ( $$LOGLEVELTOWRITEOFFSET ) ;
	"$LOGLEVELTOWRITEOFFSETglobal = " & $$LOGLEVELTOWRITEOFFSET & " ;¶"
)


// LOG LEVELS ≤ 2
// ============================================================================
& If ( theLogLevel ≤ 2 ;
	""
	& "$AllowAbortState = " & Get ( AllowAbortState ) & " ;¶"
	& "$ErrorCaptureState = " & Get ( ErrorCaptureState ) & " ;¶"
	& "$FilePath = " & Quote ( Get ( FilePath ) ) & " ;¶"
	& "$LayoutAccess = " & Get ( LayoutAccess ) & " ;¶"
	& "$LayoutName = " & Quote ( Get ( LayoutName ) ) & " ;¶"
	& "$LayoutTableName = " & Quote ( Get ( LayoutTableName ) ) & " ;¶"
	& "$LayoutViewState = " & Get ( LayoutViewState ) & " ;¶"
	& "$MultiUserState = " & Get ( MultiUserState ) & " ;¶"
	& "$RecordAccess = " & Get ( RecordAccess ) & " ;¶"
	& "$RecordID = " & Get ( RecordID ) & " ;¶"
	& "$RecordOpenCount = " & Get ( RecordOpenCount ) & " ;¶"
	& "$RecordOpenState = " &  Get ( RecordOpenState ) & " ;¶"
	& "$SortState = " & Get ( SortState ) & " ;¶"
	& "$StatusAreaState = " & Get ( StatusAreaState ) & " ;¶"
	// UserCount is disabled by default because it is slow to calculate (~1000 times slower than Get ( UserName ) )
	// & "$UserCount = " & Get ( UserCount ) & " ;¶"
	& "$WindowMode = " & Get ( WindowMode ) & " ;¶"
	& "$WindowName = " & Quote ( Get ( WindowName ) ) & " ;¶"
	& "$WindowOrientation = " & Quote ( Get ( WindowOrientation ) ) & " ;¶"
	& "$WindowStyle = " & Get ( WindowStyle ) & " ;¶"
	& "$WindowVisible = " & Get ( WindowVisible ) & " ;¶"
	& If ( not IsEmpty ( Get ( ActiveFieldName ) ) ;
		"$ActiveFieldName = " & Quote ( Get ( ActiveFieldName ) ) & " ;¶"
		& "$ActiveFieldTableName = " & Quote ( Get ( ActiveFieldTableName ) ) & " ;¶"
	)
	& If ( not IsEmpty ( Get ( ActiveLayoutObjectName ) ) ;
		"$ActiveLayoutObjectName = " & Quote ( Get ( ActiveLayoutObjectName ) ) & " ;¶"
	)
)
